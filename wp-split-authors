#!/usr/bin/env perl

# Copyright Â© 2019 Jakub Wilk <jwilk@jwilk.net>
# SPDX-License-Identifier: MIT

no lib '.';  # CVE-2016-1238

use v5.10;
use strict;
use warnings;

use English qw(-no_match_vars);

my @authors;
my $authors;
my $trailer = '';
my $xc = 0;
if (scalar @ARGV == 0) {
    local $RS = undef;
    $authors = <STDIN>;
} elsif (scalar @ARGV == 1 and $ARGV[0] eq '-x') {
    $authors = qx(xclip -o -selection clipboard) or die;
} else {
    my $prog = $PROGRAM_NAME;
    $prog =~ s{^.*/}{};
    print {*STDERR} "Usage: $prog [-x]\n";
    exit 1;
}
if ($authors =~ /;/) {
    @authors =  split(/;/, $authors);
} else {
    my $lastpos = 0;
    while ($authors =~ /(\G[^,]+,[^,]+)(,\s+|\s+$)/g) {
        push @authors, $1;
        $lastpos = $+[0];
    }
    $trailer = substr $authors, $lastpos;
}
@authors = map { s/\A\s+|\s+\z//g; $_ } @authors;
my $i = 1;
for my $author (@authors) {
    my ($last, $first) = split(/,\s+/, $author, 2);
    print "|last$i=$last|first$i=$first";
    $i++;
}
if (length $trailer > 0) {
    print "|trailer=$trailer";
    $xc = 1;
}
print "\n";
exit $xc;

# vim:ts=4 sts=4 sw=4 et
